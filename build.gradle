plugins {
    id "com.gradle.plugin-publish" version "0.9.10"
    id "groovy"
    id "idea"
}

sourceCompatibility = '1.7'
targetCompatibility = '1.7'

repositories {
    jcenter()
}

ext {
    jacksonVersion = '2.9.5'
}

dependencies {
    compile gradleApi()
    compile "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
    compile "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
}

version = "0.4-SNAPSHOT"
group = "io.github.lhotari.buildsize"

pluginBundle {
    website = 'https://github.com/lhotari/build-size-gradle-plugin'
    vcsUrl = 'https://github.com/lhotari/build-size-gradle-plugin'
    description = '''Plugin for generating JSON that shows build size and structure but doesn't reveal content. Names are masked.'''
    tags = ['performance', 'testing']

    plugins {
        buildSizePlugin {
            id = 'io.github.lhotari.buildsize'
            displayName = 'Build size information'
        }
    }
}

task generateScriptPlugin {
    inputs.file file("src/main/groovy/io/github/lhotari/buildsize/BuildSizePlugin.groovy")
    def outputFile = new File(buildDir, "buildSize.gradle")
    outputs.file outputFile
    doLast {
        copy {
            from 'src/main/groovy/io/github/lhotari/buildsize/BuildSizePlugin.groovy'
            into buildDir
            rename 'BuildSizePlugin.groovy', 'buildSize.gradle'
            filter { String line ->
                line.trim().startsWith('package io.github.lhotari.buildsize') ? null : line
            }
        }
        outputFile.text = """
buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "com.fasterxml.jackson.core:jackson-core:${jacksonVersion}"
        classpath "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
    }
}
        """ + outputFile.text + '''
apply plugin: BuildSizePlugin        
'''
    }
}
